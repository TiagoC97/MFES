class Client is subclass of User
types

values
	
instance variables

	private trainer: [Trainer];
	private personalTrainingFee: rat;
	private classes: set of GymClass := {};
	private trainingSessions: set of TrainingSession := {};
	private gymAttendences: set of Date := {};
	private productsBought: seq of Product := [];

	private gymFeePayments: set of GymFeePayment := {};
	private productPayments: set of ProductPayment := {};
	private personalTrainingPayments: set of PersonalTrainingPayment := {};

	private historyGymFeePayments: set of GymFeePayment := {};
	private historyProductPayments: set of ProductPayment := {};
	private historyPersonalTrainingPayments: set of PersonalTrainingPayment := {};
	
operations

	/**
	 * Client constructor
	 */
	public Client: String_ * nat * Gender * String1 ==> Client
	 Client(newName, newAge, newGender, newNationality) == 
	 (
	 	trainer := nil;
	 	personalTrainingFee := 0;
	 	User(newName, <Client>, newAge, newGender, newNationality);
	 )
	 post trainer = nil and personalTrainingFee = 0 and classes = {} and trainingSessions = {} and gymAttendences = {} and productsBought = [] and
	 	gymFeePayments = {} and productPayments = {} and personalTrainingPayments = {} and historyGymFeePayments = {} and
		historyProductPayments = {} and historyPersonalTrainingPayments = {};
	 
	 /**
	  * Adds a trainer to the client
	  */
	public addTrainer: Trainer * rat==> ()
	addTrainer(newTrainer, fee) == 
	(
		trainer := newTrainer;
		personalTrainingFee := fee;			
	)
	pre newTrainer in set club.getTrainers() and fee > 0
	post trainer = newTrainer;
	
	-- MESSAGES TO/FROM GROUP
	
	 	/**
	 * Send message to one of its groups
	 */
	public sendMessageToGroup: String1 * String1 ==> ()
	sendMessageToGroup(msg, groupName) == club.getGroupByName(groupName).sendMessage(self, msg)
	pre groupName in set dom club.getGroups() and self in set club.getClients();
 	
 	-- get the messages from one of its groups
 	public pure readGroupMessages: String1 ==> map String1 to seq of String1
	readGroupMessages(groupName) == return club.getGroupByName(groupName).checkInbox(self)
	pre self in set club.getClients() and
	groupName in set dom club.getGroups();
	
	-- get the offers from one of its groups
 	public pure readGroupOffers: String1 ==> seq of String1
	readGroupOffers(groupName) == return club.getGroupByName(groupName).checkOffers(self)
	pre self in set club.getClients() and
	groupName in set dom club.getGroups();
	
	-- get the messages, from one of its groups, sent by a given user
 	public pure readGroupMessagesFromUser: String1 * User ==> seq of String1
	readGroupMessagesFromUser(groupName, user) == return club.getGroupByName(groupName).getMessagesFromUser(user.getName(), self)
	pre 
		self in set club.getClients() and
		groupName in set dom club.getGroups() and
		club.getGroupByName(groupName).getMessagesFromUser(user.getName(), self) <> [] and
		user in set club.getUsers()
	post RESULT = club.getGroupByName(groupName).getMessagesFromUser(user.getName(), self);
	
	-- get the last message, from one of its groups, sent by a given user
 	public pure readGroupLastMessageFromUser: String1 * User ==> String1
	readGroupLastMessageFromUser(groupName, user) == return club.getGroupByName(groupName).getLastMessageFromUser(user.getName(), self)
	pre 
		self in set club.getClients() and
		groupName in set dom club.getGroups() and
		club.getGroupByName(groupName).getMessagesFromUser(user.getName(), self) <> [] and
		user in set club.getUsers()
	post RESULT = club.getGroupByName(groupName).getLastMessageFromUser(user.getName(), self);
	
	
	-- GYM CLASSES
	
	public addGymClass: GymClass ==> ()
	addGymClass(gymClass) == 
	(
		dcl classDate: Date := gymClass.getDate();
		classes := classes union {gymClass};
		addGymAttendence(classDate);
	)		
	pre gymClass not in set classes
	post classes = classes~ union {gymClass};

	-- TRAINING SESSIONS
	
	public addTrainingSession: TrainingSession ==> ()
	addTrainingSession(trainingSession) ==
	(
		dcl trainingSessionDate: Date := trainingSession.getDate();	
		trainingSessions := trainingSessions union {trainingSession};  
		addGymAttendence(trainingSessionDate);
	) 
	pre trainingSession not in set trainingSessions
	post trainingSessions = trainingSessions~ union {trainingSession};

	-- GYM ATTENDENCES

	public addGymAttendence: Date ==> ()
	addGymAttendence(date) == gymAttendences := gymAttendences union {date}
	pre date not in set gymAttendences
	post gymAttendences = gymAttendences~ union {date};

	-- PAYMENTS

	public addGymFeePayment: GymFeePayment ==> ()
	addGymFeePayment(payment) == gymFeePayments := gymFeePayments union {payment};

	public addProductPayment: ProductPayment ==> ()
	addProductPayment(payment) == productPayments := productPayments union {payment};

	public addPersonalTrainingPayment: PersonalTrainingPayment ==> ()
	addPersonalTrainingPayment(payment) == personalTrainingPayments := personalTrainingPayments union {payment};

	public addHistoryGymFeePayment: GymFeePayment ==> ()
	addHistoryGymFeePayment(payment) == historyGymFeePayments := historyGymFeePayments union {payment};

	public addHistoryProductPayment: ProductPayment ==> ()
	addHistoryProductPayment(payment) == historyProductPayments := historyProductPayments union {payment};

	public addHistoryPersonalTrainingPayment: PersonalTrainingPayment ==> ()
	addHistoryPersonalTrainingPayment(payment) == historyPersonalTrainingPayments := historyPersonalTrainingPayments union {payment};
	
	public removeGymFeePayment: GymFeePayment ==> ()
	removeGymFeePayment(payment) == 
	(
		addHistoryGymFeePayment(payment);
		gymFeePayments := gymFeePayments \ {payment};
	)
	pre gymFeePayments <> {} and payment in set gymFeePayments;

	public removeProductPayment: ProductPayment ==> ()
	removeProductPayment(payment) == 
	(
		addHistoryProductPayment(payment);		
		productPayments := productPayments \ {payment};
	)
	pre productPayments <> {} and payment in set productPayments;

	public removePersonalTrainingPayment: PersonalTrainingPayment ==> ()
	removePersonalTrainingPayment(payment) == 
	(
		addHistoryPersonalTrainingPayment(payment);		
		personalTrainingPayments := personalTrainingPayments \ {payment};
	)
	pre personalTrainingPayments <> {} and payment in set personalTrainingPayments;

	public moveAllGymFeePaymentsToHistory: () ==> ()
	moveAllGymFeePaymentsToHistory() == 
	(
		for all payment in set gymFeePayments do
			removeGymFeePayment(payment);
	)
	post gymFeePayments = {};

	public moveAllProductPaymentsToHistory: () ==> ()
	moveAllProductPaymentsToHistory() == 
	(
		for all payment in set productPayments do
		(
			removeProductPayment(payment);
		)
	)
	post productPayments = {};

	public moveAllPersonalTrainingPaymentsToHistory: () ==> ()
	moveAllPersonalTrainingPaymentsToHistory() == 
	(
		for all payment in set personalTrainingPayments do
		(
			removePersonalTrainingPayment(payment);
		);
	)
	post personalTrainingPayments = {};

	public payGymFee: Date * Hour ==> ()
	payGymFee(date, hour) == 
	(
		dcl payment: GymFeePayment := new GymFeePayment(self, club.getFee(), date, hour);
		addGymFeePayment(payment);
	);

	public payPersonalTrainingFee: Date * Hour ==> ()
	payPersonalTrainingFee(date, hour) == 
	(
		dcl payment: PersonalTrainingPayment := new PersonalTrainingPayment(self, club.getFee(), date, hour);
		addPersonalTrainingPayment(payment);
	);
	-- PRODUCTS

	public buyProducts: seq of Product * Date * Hour ==> ()
	buyProducts(products, date, hour) == 
	(
		dcl payment: ProductPayment := new ProductPayment(self, products, date, hour);
		addProductPayment(payment);
		productsBought:= productsBought ^ products;
	);

	

	-- GETTERS

	/**
	 * Get client activity
	 */

	 public getActivity: () ==> ()
		getActivity() ==
		(
			dcl numClasses: nat := card classes;
			dcl numTrainingSessiosn: nat := card trainingSessions;
			dcl numAttendences: nat := card gymAttendences;
			dcl personalTrainer: String_ := "None";
			
			if trainer <> nil then
				personalTrainer:= trainer.getName();
			

			IO`println("********* CLIENT STATISTICS *********");
			IO`println("Personal trainer: " ^ [personalTrainer]);
			IO`println("Number of gym classes: " ^ [numClasses]);
			IO`println("Number of training sessions: " ^ [numTrainingSessiosn]);
			IO`println("Number of gym attendences: " ^ [numAttendences]);
			IO`println("");
			IO`println("************************************");
		);
	
	/**
	 * Gets the client trainer
	 * 
	 * @return trainer
	 */
	public pure getTrainer: () ==> Trainer
	getTrainer() == return trainer
	post RESULT = trainer;

	/**
	 * Gets the client personal training fee
	 * 
	 * @return personalTrainingFee
	 */
	public pure getPersonalTrainingFee: () ==> rat
	getPersonalTrainingFee() == return personalTrainingFee
	post RESULT = personalTrainingFee;

	/**
	 * Gets the client classes
	 * 
	 * @return set of GymClass
	 */
	public pure getClasses: () ==> set of GymClass
	getClasses() == return classes
	post RESULT = classes;

	/**
	 * Gets the client training sessions
	 * 
	 * @return set of TrainingSession
	 */
	public pure getTrainingSessions: () ==> set of TrainingSession
	getTrainingSessions() == return trainingSessions
	post RESULT = trainingSessions;

	/**
	 * Gets the client gym attendences
	 * 
	 * @return set of Date
	 */
	public pure getGymAttendences: () ==> set of Date
	getGymAttendences() == return gymAttendences
	post RESULT = gymAttendences;

	/**
	 * Gets the client products bought
	 * 
	 * @return seq of Product
	 */
	public pure getProductsBought: () ==> seq of Product
	getProductsBought() == return productsBought
	post RESULT = productsBought;

	/**
	 * Gets the client gym fee paymenrs
	 * 
	 * @return set of GymFeePayment
	 */
	public pure getGymFeePayments: () ==> set of GymFeePayment
	getGymFeePayments() == return gymFeePayments
	post RESULT = gymFeePayments;

	/**
	 * Gets the client product payments
	 * 
	 * @return set of ProductPayment
	 */
	public pure getProductPayments: () ==> set of ProductPayment
	getProductPayments() == return productPayments
	post RESULT = productPayments;
	
	/**
	 * Gets the client personal training payments
	 * 
	 * @return set of PersonalTrainingPayment
	 */
	public pure getPersonalTrainingPayments: () ==> set of PersonalTrainingPayment
	getPersonalTrainingPayments() == return personalTrainingPayments
	post RESULT = personalTrainingPayments;


	/**
	 * Gets the client history of gym fee payments
	 * 
	 * @return set of GymFeePayment
	 */
	public pure getHistoryGymFeePayments: () ==> set of GymFeePayment
	getHistoryGymFeePayments() == return historyGymFeePayments
	post RESULT = historyGymFeePayments;

	/**
	 * Gets the client history of product payments
	 * 
	 * @return set of ProductPayment
	 */
	public pure getHistoryProductPayments: () ==> set of ProductPayment
	getHistoryProductPayments() == return historyProductPayments
	post RESULT = historyProductPayments;

	/**
	 * Gets the client history of personal training payments
	 * 
	 * @return set of PersonalTrainingPayment
	 */
	public pure getHistoryPersonalTrainingPayments: () ==> set of PersonalTrainingPayment
	getHistoryPersonalTrainingPayments() == return historyPersonalTrainingPayments
	post RESULT = historyPersonalTrainingPayments;
		
	/**
	 * Gets the client active payments of a given type
	 * 
	 * @return set of Payment
	 */
	public pure getPaymentsOfGivenType: String1 ==> set of Payment
	getPaymentsOfGivenType(type) ==
	(
		cases type:
			"product" -> return productPayments,
			"gymFee" -> return gymFeePayments,
			"personalTraining" -> return personalTrainingPayments
		end;
		return {};
	)
	pre type in set {"product", "gymFee", "personalTraining"};

end Client